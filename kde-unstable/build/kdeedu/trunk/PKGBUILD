# $Id: PKGBUILD 17572 2008-10-31 03:06:35Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdeedu
pkgver=4.1.72
pkgrel=1
pkgdesc="KDE Educational programs"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdebase-workspace' 'openbabel' 'indilib' 'libnova' 'libqalculate' 'gsl' 'boost')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ocaml' 'facile' 'eigen' 'cfitsio' 'gmm++')
options=('docs')
install='kdeedu.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('755cef6555a0c1f27929147be8d2d84a')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_kstars=OFF	# due to missing file
	make
	make DESTDIR=$pkgdir install
	# provided by indilib
	rm -f $pkgdir/usr/bin/indiserver
}
