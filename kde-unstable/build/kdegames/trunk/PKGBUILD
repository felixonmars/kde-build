# $Id: PKGBUILD 17573 2008-10-31 03:06:39Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdegames
pkgver=4.1.72
pkgrel=1
pkgdesc="KDE Games"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'ggz-client-libs' 'kdebase-runtime')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
install='kdegames.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('34680d4dca2bab916dd5056e58a951ec')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
	# will be generated during install
	rm -rf $pkgdir/etc
	install -D -m644 $srcdir/${pkgname}-${pkgver}/kreversi/module.dsc $pkgdir/usr/share/ggz/kreversi.dsc
	install -D -m644 $srcdir/${pkgname}-${pkgver}/ksquares/src/module.dsc $pkgdir/usr/share/ggz/ksquares.dsc
	install -D -m644 $srcdir/${pkgname}-${pkgver}/kbattleship/src/module.dsc $pkgdir/usr/share/ggz/kbattleship.dsc
}
